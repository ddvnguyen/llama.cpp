#include "common.cuh"
#include "fattn-common.cuh"
#include "fattn-mma-f16.cuh"
#include "fattn-tile.cuh"
#include "fattn-vec.cuh"
#include "fattn.cuh"

#include <algorithm>
#include <limits>
#include <unordered_map>
#include <vector>

#if !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)
__launch_bounds__(256, 1)
static __global__ void flash_attn_mask_to_sparse_indices(
        const half * mask_ptr, int32_t * indices_ptr, const int ne30, const int n_kv_max,
        const int64_t s31, const int64_t s33) {
    ggml_cuda_pdl_sync();

    constexpr int values_per_lane = 8;
    const int tid      = threadIdx.x;
    const int warp     = tid / WARP_SIZE;
    const int lane     = tid % WARP_SIZE;
    const int sequence = blockIdx.y;
    const int query    = blockIdx.x;

    const half * mask = mask_ptr + sequence*s33 + query*s31;
    int32_t * indices = indices_ptr + (int64_t(sequence)*gridDim.x + query)*n_kv_max;

    __shared__ int warp_offsets[256/WARP_SIZE];
    __shared__ int row_count;
    __shared__ int chunk_count;

    if (tid == 0) {
        row_count = 0;
    }
    __syncthreads();

    for (int i0 = 0; i0 < ne30; i0 += blockDim.x*values_per_lane) {
        uint32_t selected_warp[values_per_lane];
        int warp_count = 0;
#pragma unroll
        for (int item = 0; item < values_per_lane; ++item) {
            const int i = i0 + (warp*values_per_lane + item)*WARP_SIZE + lane;
            const bool selected = i < ne30 && isfinite(__half2float(mask[i]));
            selected_warp[item] = __ballot_sync(0xFFFFFFFF, selected);
            warp_count += __popc(selected_warp[item]);
        }

        if (lane == 0) {
            warp_offsets[warp] = warp_count;
        }
        __syncthreads();

        if (tid == 0) {
            int offset = 0;
#pragma unroll
            for (int iw = 0; iw < 256/WARP_SIZE; ++iw) {
                const int count = warp_offsets[iw];
                warp_offsets[iw] = offset;
                offset += count;
            }
            chunk_count = offset;
        }
        __syncthreads();

        const uint32_t lane_mask = lane == 0 ? 0 : (1u << lane) - 1;
        int warp_item_offset = 0;
#pragma unroll
        for (int item = 0; item < values_per_lane; ++item) {
            const int i = i0 + (warp*values_per_lane + item)*WARP_SIZE + lane;
            const int dst = row_count + warp_offsets[warp] + warp_item_offset + __popc(selected_warp[item] & lane_mask);
            if ((selected_warp[item] & (uint32_t(1) << lane)) && dst < n_kv_max) {
                indices[dst] = i;
            }
            warp_item_offset += __popc(selected_warp[item]);
        }
        __syncthreads();

        if (tid == 0) {
            row_count += chunk_count;
        }
        __syncthreads();
    }

    const int count = row_count;
    for (int i = count + tid; i < n_kv_max; i += blockDim.x) {
        indices[i] = -1;
    }
    __syncthreads();

    // the dependent grid reads indices, signal once the row is complete
    ggml_cuda_pdl_lc();
}
#endif // !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)

void ggml_cuda_flash_attn_ext_compact_mask(
        const ggml_tensor * mask, int32_t * indices, int32_t n_kv_max, cudaStream_t stream) {
#if defined(GGML_USE_HIP) || defined(GGML_USE_MUSA)
    GGML_UNUSED_VARS(mask, indices, n_kv_max, stream);
    GGML_ABORT("sparse flash attention is only supported on NVIDIA CUDA");
#else
    const int64_t s31 = mask->nb[1] / sizeof(half);
    const int64_t s33 = mask->nb[3] / sizeof(half);
    const dim3 blocks_num(mask->ne[1], mask->ne[3], 1);
    const dim3 block_dim(256, 1, 1);
    const ggml_cuda_kernel_launch_params launch_params(blocks_num, block_dim, 0, stream);
    ggml_cuda_kernel_launch(flash_attn_mask_to_sparse_indices, launch_params,
        (const half *) mask->data, indices, int(mask->ne[0]), n_kv_max, s31, s33);
    CUDA_CHECK(cudaGetLastError());
#endif // !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)
}

bool ggml_cuda_flash_attn_ext_mma_f16_shall_use_sparse(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
#if defined(GGML_USE_HIP) || defined(GGML_USE_MUSA)
    GGML_UNUSED_VARS(ctx, dst);
    return false;
#else
    const ggml_tensor * Q    = dst->src[0];
    const ggml_tensor * K    = dst->src[1];
    const ggml_tensor * mask = dst->src[3];
    const int cc = ggml_cuda_info().devices[ctx.device].cc;

    float max_bias = 0.0f;
    float logit_softcap = 0.0f;
    memcpy(&max_bias,      (const float *) dst->op_params + 1, sizeof(float));
    memcpy(&logit_softcap, (const float *) dst->op_params + 2, sizeof(float));

    const int32_t n_kv_max = ggml_get_op_params_i32(dst, 4);
    return GGML_CUDA_CC_IS_NVIDIA(cc) && turing_mma_available(cc) &&
        mask != nullptr && n_kv_max > 0 && max_bias == 0.0f && logit_softcap == 0.0f &&
        mask->ne[0] == K->ne[1] && mask->ne[1] >= Q->ne[1] && mask->ne[2] == 1 &&
        K->ne[1] >= std::max<int64_t>(4096, 2LL*n_kv_max);
#endif // !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)
}
struct ggml_cuda_kv_stream_resident_cache;

namespace {

constexpr size_t KV_STREAM_NO_REQUEST = std::numeric_limits<size_t>::max();
constexpr uint32_t KV_STREAM_NO_LAYER = std::numeric_limits<uint32_t>::max();

struct kv_stream_graph_request {
    const char * k_data = nullptr;
    const char * v_data = nullptr;
    size_t k_nb1 = 0;
    size_t k_nb2 = 0;
    size_t v_nb1 = 0;
    size_t v_nb2 = 0;
    int64_t n_head_kv = 0;
    int64_t token_begin = 0;
    int64_t token_count = 0;
    size_t k_row_bytes = 0;
    size_t v_row_bytes = 0;
    size_t k_head_bytes = 0;
    size_t k_bytes = 0;
    size_t v_offset = 0;
    size_t v_head_bytes = 0;
    size_t v_bytes = 0;
    uint32_t layer = 0;
    uint32_t slot = 0;
    bool mutable_tail = false;
    bool eligible = false;
    bool scheduled = false;
    bool consumed = false;
    bool deadline_sample = false;
};

} // namespace

struct ggml_cuda_kv_stream_transfer_ring {
    char * pool_data = nullptr;
    size_t page_bytes = 0;
    uint32_t capacity_slots = 0;
    uint32_t active_slots = 0;
    cudaStream_t copy_stream = nullptr;
    cudaEvent_t producer_ready = nullptr;
    cudaEvent_t eval_start = nullptr;
    cudaEvent_t eval_end = nullptr;
    cudaEvent_t copy_sample_start = nullptr;
    cudaEvent_t copy_sample_end = nullptr;
    std::vector<cudaEvent_t> ready;
    std::vector<cudaEvent_t> consumed;
    std::vector<uint8_t> slot_used;
    std::vector<size_t> slot_request;
    uint32_t * ready_flags_host = nullptr;
    uint32_t * ready_flags_device = nullptr;
    uint64_t * deadline_counters_host = nullptr;
    uint64_t * deadline_counters_device = nullptr;

    bool graph_active = false;
    uint32_t graph_layer_count = 0;
    uint32_t current_layer = KV_STREAM_NO_LAYER;
    size_t next_request = 0;
    ggml_cuda_kv_stream_resident_cache * graph_resident_cache = nullptr;
    std::vector<kv_stream_graph_request> graph_requests;
    std::unordered_map<const void *, uint32_t> graph_layer_by_k;
    std::unordered_map<const void *, std::vector<size_t>> graph_request_by_k_page;

    uint64_t asynchronous_page_uploads = 0;
    uint64_t coalesced_upload_batches = 0;
    uint64_t coalesced_upload_pages = 0;
    uint64_t compute_stream_waits = 0;
    uint64_t stage_slot_reuses = 0;
    uint64_t cross_layer_prefetches = 0;
    uint32_t current_occupancy = 0;
    uint32_t ring_peak_occupancy = 0;
    uint32_t current_ring_peak_occupancy = 0;
    uint32_t last_ring_peak_occupancy = 0;
    uint64_t current_epoch_uploads = 0;
    double last_copy_engine_busy_ratio = 0.0;
    bool timing_pending = false;
    bool timing_current = false;
    bool copy_sample_recorded = false;
    uint32_t copy_sample_pages = 0;
};

ggml_cuda_kv_stream_transfer_ring * ggml_cuda_kv_stream_transfer_ring_new(
        void * pool_data, size_t page_bytes, uint32_t stage_slots) {
    if (pool_data == nullptr || page_bytes == 0 || stage_slots == 0) {
        return nullptr;
    }

    auto * ring = new ggml_cuda_kv_stream_transfer_ring;
    ring->pool_data = static_cast<char *>(pool_data);
    ring->page_bytes = page_bytes;
    ring->capacity_slots = stage_slots;
    ring->active_slots = stage_slots;
    ring->ready.resize(stage_slots, nullptr);
    ring->consumed.resize(stage_slots, nullptr);
    ring->slot_used.resize(stage_slots, 0);
    ring->slot_request.resize(stage_slots, KV_STREAM_NO_REQUEST);

    auto cleanup = [&]() {
        for (cudaEvent_t event : ring->ready) {
            if (event != nullptr) {
                (void) cudaEventDestroy(event);
            }
        }
        for (cudaEvent_t event : ring->consumed) {
            if (event != nullptr) {
                (void) cudaEventDestroy(event);
            }
        }
        if (ring->producer_ready != nullptr) {
            (void) cudaEventDestroy(ring->producer_ready);
        }
        if (ring->eval_start != nullptr) {
            (void) cudaEventDestroy(ring->eval_start);
        }
        if (ring->eval_end != nullptr) {
            (void) cudaEventDestroy(ring->eval_end);
        }
        if (ring->copy_sample_start != nullptr) {
            (void) cudaEventDestroy(ring->copy_sample_start);
        }
        if (ring->copy_sample_end != nullptr) {
            (void) cudaEventDestroy(ring->copy_sample_end);
        }
        if (ring->copy_stream != nullptr) {
            (void) cudaStreamDestroy(ring->copy_stream);
        }
        if (ring->ready_flags_host != nullptr) {
            (void) cudaFreeHost(ring->ready_flags_host);
        }
        if (ring->deadline_counters_host != nullptr) {
            (void) cudaFreeHost(ring->deadline_counters_host);
        }
        delete ring;
    };

    if (cudaHostAlloc(reinterpret_cast<void **>(&ring->ready_flags_host),
            stage_slots*sizeof(uint32_t), cudaHostAllocMapped) != cudaSuccess ||
        cudaHostGetDevicePointer(reinterpret_cast<void **>(&ring->ready_flags_device),
            ring->ready_flags_host, 0) != cudaSuccess ||
        cudaHostAlloc(reinterpret_cast<void **>(&ring->deadline_counters_host),
            2*sizeof(uint64_t), cudaHostAllocMapped) != cudaSuccess ||
        cudaHostGetDevicePointer(reinterpret_cast<void **>(&ring->deadline_counters_device),
            ring->deadline_counters_host, 0) != cudaSuccess) {
        (void) cudaGetLastError();
        cleanup();
        return nullptr;
    }
    std::fill_n(ring->ready_flags_host, stage_slots, 0u);
    std::fill_n(ring->deadline_counters_host, 2, uint64_t(0));

    if (cudaStreamCreateWithFlags(&ring->copy_stream, cudaStreamNonBlocking) != cudaSuccess ||
        cudaEventCreateWithFlags(&ring->producer_ready, cudaEventDisableTiming) != cudaSuccess ||
        cudaEventCreate(&ring->eval_start) != cudaSuccess ||
        cudaEventCreate(&ring->eval_end) != cudaSuccess ||
        cudaEventCreate(&ring->copy_sample_start) != cudaSuccess ||
        cudaEventCreate(&ring->copy_sample_end) != cudaSuccess) {
        (void) cudaGetLastError();
        cleanup();
        return nullptr;
    }
    for (uint32_t slot = 0; slot < stage_slots; ++slot) {
        if (cudaEventCreateWithFlags(&ring->ready[slot], cudaEventDisableTiming) != cudaSuccess ||
            cudaEventCreateWithFlags(&ring->consumed[slot], cudaEventDisableTiming) != cudaSuccess) {
            (void) cudaGetLastError();
            cleanup();
            return nullptr;
        }
    }
    return ring;
}

void ggml_cuda_kv_stream_transfer_ring_free(ggml_cuda_kv_stream_transfer_ring * ring) {
    if (ring == nullptr) {
        return;
    }
    CUDA_CHECK(cudaStreamSynchronize(ring->copy_stream));
    for (cudaEvent_t event : ring->ready) {
        CUDA_CHECK(cudaEventDestroy(event));
    }
    for (cudaEvent_t event : ring->consumed) {
        CUDA_CHECK(cudaEventDestroy(event));
    }
    CUDA_CHECK(cudaEventDestroy(ring->producer_ready));
    CUDA_CHECK(cudaEventDestroy(ring->eval_start));
    CUDA_CHECK(cudaEventDestroy(ring->eval_end));
    CUDA_CHECK(cudaEventDestroy(ring->copy_sample_start));
    CUDA_CHECK(cudaEventDestroy(ring->copy_sample_end));
    CUDA_CHECK(cudaStreamDestroy(ring->copy_stream));
    CUDA_CHECK(cudaFreeHost(ring->ready_flags_host));
    CUDA_CHECK(cudaFreeHost(ring->deadline_counters_host));
    delete ring;
}

bool ggml_cuda_kv_stream_transfer_ring_set_active_slots(
        ggml_cuda_kv_stream_transfer_ring * ring, uint32_t stage_slots) {
    if (ring == nullptr || stage_slots == 0 || stage_slots > ring->capacity_slots) {
        return false;
    }
    ring->active_slots = stage_slots;
    return true;
}

ggml_cuda_kv_stream_transfer_stats ggml_cuda_kv_stream_transfer_ring_get_stats(
        const ggml_cuda_kv_stream_transfer_ring * ring) {
    if (ring == nullptr) {
        return {};
    }
    return {
        ring->asynchronous_page_uploads,
        ring->coalesced_upload_batches,
        ring->coalesced_upload_pages,
        ring->compute_stream_waits,
        ring->stage_slot_reuses,
        ring->cross_layer_prefetches,
        ring->deadline_counters_host[0],
        ring->deadline_counters_host[1],
        ring->ring_peak_occupancy,
    };
}

struct ggml_cuda_kv_stream_resident_cache {
    char * pool_data = nullptr;
    size_t pool_bytes = 0;
    size_t scratch_bytes = 0;
    size_t page_bytes = 0;
    uint32_t layer_count = 0;
    uint32_t page_tokens = 0;
    uint32_t resident_pages_per_layer = 0;
    uint32_t next_layer = 0;

    std::unordered_map<const void *, uint32_t> layer_by_k;
    std::vector<uint8_t> loaded;
    ggml_cuda_kv_stream_resident_stats stats;
};

ggml_cuda_kv_stream_resident_cache * ggml_cuda_kv_stream_resident_cache_new(
        void * pool_data, size_t pool_bytes, size_t scratch_bytes, size_t page_bytes,
        uint32_t layer_count, uint32_t page_tokens) {
    if (pool_data == nullptr || scratch_bytes == 0 || scratch_bytes >= pool_bytes ||
            page_bytes == 0 || scratch_bytes%page_bytes != 0 ||
            layer_count == 0 || page_tokens != 256) {
        return nullptr;
    }

    const size_t resident_pages = (pool_bytes - scratch_bytes)/(page_bytes*layer_count);
    if (resident_pages == 0 || resident_pages > UINT32_MAX) {
        return nullptr;
    }

    auto * cache = new ggml_cuda_kv_stream_resident_cache;
    cache->pool_data = static_cast<char *>(pool_data);
    cache->pool_bytes = pool_bytes;
    cache->scratch_bytes = scratch_bytes;
    cache->page_bytes = page_bytes;
    cache->layer_count = layer_count;
    cache->page_tokens = page_tokens;
    cache->resident_pages_per_layer = resident_pages;
    cache->loaded.resize(size_t(layer_count)*resident_pages, 0);
    return cache;
}

void ggml_cuda_kv_stream_resident_cache_free(ggml_cuda_kv_stream_resident_cache * cache) {
    delete cache;
}

void ggml_cuda_kv_stream_resident_cache_reset(ggml_cuda_kv_stream_resident_cache * cache) {
    if (cache == nullptr) {
        return;
    }
    std::fill(cache->loaded.begin(), cache->loaded.end(), 0);
    cache->layer_by_k.clear();
    cache->next_layer = 0;
    cache->stats = {};
}

bool ggml_cuda_kv_stream_resident_cache_repartition(
        ggml_cuda_kv_stream_resident_cache * cache, size_t scratch_bytes) {
    if (cache == nullptr || scratch_bytes > cache->pool_bytes ||
        scratch_bytes%cache->page_bytes != 0) {
        return false;
    }
    const size_t pages = (cache->pool_bytes - scratch_bytes)/
        (cache->page_bytes*cache->layer_count);
    if (pages > UINT32_MAX) {
        return false;
    }
    cache->scratch_bytes = scratch_bytes;
    cache->resident_pages_per_layer = uint32_t(pages);
    cache->loaded.assign(size_t(cache->layer_count)*pages, 0);
    cache->layer_by_k.clear();
    cache->next_layer = 0;
    cache->stats = {};
    return true;
}

uint32_t ggml_cuda_kv_stream_resident_cache_pages_per_layer(
        const ggml_cuda_kv_stream_resident_cache * cache) {
    return cache == nullptr ? 0 : cache->resident_pages_per_layer;
}

ggml_cuda_kv_stream_resident_stats ggml_cuda_kv_stream_resident_cache_get_stats(
        const ggml_cuda_kv_stream_resident_cache * cache) {
    return cache == nullptr ? ggml_cuda_kv_stream_resident_stats{} : cache->stats;
}

static uint32_t kv_stream_resident_layer(
        ggml_cuda_kv_stream_resident_cache * cache, const void * k_key) {
    GGML_ASSERT(cache != nullptr);
    auto [it, inserted] = cache->layer_by_k.emplace(k_key, cache->next_layer);
    if (inserted) {
        GGML_ASSERT(cache->next_layer < cache->layer_count);
        ++cache->next_layer;
    }
    return it->second;
}

namespace {

constexpr int KV_STREAM_HEAD_DIM = 256;
constexpr int KV_STREAM_PARTS_PER_CHUNK = 2;

static int64_t kv_stream_block_tokens(const ggml_tensor * dst, size_t stage_bytes) {
    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];

    const size_t k_row_bytes = ggml_row_size(K->type, K->ne[0]);
    const size_t v_row_bytes = ggml_row_size(V->type, V->ne[0]);
    const size_t bytes_per_token = k_row_bytes*K->ne[2] + v_row_bytes*V->ne[2];
    if (bytes_per_token == 0) {
        return 0;
    }

    int64_t tokens = std::min<int64_t>(K->ne[1], stage_bytes/bytes_per_token);
    tokens = tokens/FATTN_KQ_STRIDE*FATTN_KQ_STRIDE;
    while (tokens > 0) {
        const size_t k_bytes = k_row_bytes*tokens*K->ne[2];
        const size_t v_offset = GGML_PAD(k_bytes, 128);
        const size_t v_bytes = v_row_bytes*tokens*V->ne[2];
        if (v_offset <= stage_bytes && v_bytes <= stage_bytes - v_offset) {
            return tokens;
        }
        tokens -= FATTN_KQ_STRIDE;
    }
    return 0;
}

template<int D>
static __global__ void kv_stream_accumulate_chunk_results(
        const float * parts,
        const float2 * meta,
        float * accumulator,
        float2 * accumulator_meta,
        int nrows,
        bool initialize) {
    ggml_cuda_pdl_lc();
    const int row = blockIdx.x;
    const int tid = threadIdx.x;
    if (row >= nrows || tid >= D) {
        return;
    }
    ggml_cuda_pdl_sync();

    __shared__ float old_maximum;
    __shared__ float maximum;
    __shared__ float old_scale;
    __shared__ float denominator;

    const int base = row*KV_STREAM_PARTS_PER_CHUNK;
    if (tid == 0) {
        old_maximum = initialize ? -FLT_MAX : accumulator_meta[row].x;
        maximum = old_maximum;
        for (int part = 0; part < KV_STREAM_PARTS_PER_CHUNK; ++part) {
            maximum = fmaxf(maximum, meta[base + part].x);
        }

        old_scale = initialize ? 0.0f : expf(old_maximum - maximum);
        denominator = initialize ? 0.0f : old_scale*accumulator_meta[row].y;
        for (int part = 0; part < KV_STREAM_PARTS_PER_CHUNK; ++part) {
            const float weight = expf(meta[base + part].x - maximum);
            denominator += weight*meta[base + part].y;
        }
        accumulator_meta[row] = make_float2(maximum, denominator);
    }
    __syncthreads();

    float numerator = initialize ? 0.0f : old_scale*accumulator[row*D + tid];
    for (int part = 0; part < KV_STREAM_PARTS_PER_CHUNK; ++part) {
        const float weight = expf(meta[base + part].x - maximum);
        numerator += weight*parts[(base + part)*D + tid];
    }
    accumulator[row*D + tid] = numerator;
}

template<int D>
static __global__ void kv_stream_normalize_chunk_results(
        const float * accumulator,
        const float2 * accumulator_meta,
        float * dst,
        int nrows) {
    ggml_cuda_pdl_lc();
    const int row = blockIdx.x;
    const int tid = threadIdx.x;
    if (row >= nrows || tid >= D) {
        return;
    }
    ggml_cuda_pdl_sync();

    dst[row*D + tid] = accumulator[row*D + tid]/accumulator_meta[row].y;
}

template<bool use_logit_softcap>
static void kv_stream_launch_q8_q4_partial(
        ggml_backend_cuda_context & ctx,
        ggml_tensor * dst,
        float * parts,
        float2 * meta) {
    const ggml_tensor * Q = dst->src[0];
    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];
    const ggml_tensor * mask = dst->src[3];

    constexpr int ncols = 1;
    constexpr int nthreads = 128;
    constexpr int nwarps = nthreads/WARP_SIZE;
    fattn_kernel_t kernel = flash_attn_ext_vec<
        KV_STREAM_HEAD_DIM, ncols, GGML_TYPE_Q8_0, GGML_TYPE_Q4_0, use_logit_softcap>;

    float scale = 1.0f;
    float max_bias = 0.0f;
    float logit_softcap = 0.0f;
    memcpy(&scale,         (const float *) dst->op_params + 0, sizeof(float));
    memcpy(&max_bias,      (const float *) dst->op_params + 1, sizeof(float));
    memcpy(&logit_softcap, (const float *) dst->op_params + 2, sizeof(float));
    if (logit_softcap != 0.0f) {
        scale /= logit_softcap;
    }

    const uint32_t n_head = Q->ne[2];
    const uint32_t n_head_log2 = 1u << uint32_t(floorf(log2f(float(n_head))));
    const float m0 = powf(2.0f, -(max_bias       )/n_head_log2);
    const float m1 = powf(2.0f, -(max_bias/2.0f)/n_head_log2);
    const uint3 ne01 = init_fastdiv_values(Q->ne[1]);

    const dim3 blocks(Q->ne[1], KV_STREAM_PARTS_PER_CHUNK, Q->ne[2]*Q->ne[3]);
    const dim3 threads(WARP_SIZE, nwarps, 1);
    const ggml_cuda_kernel_launch_params launch_params(blocks, threads, 0, ctx.stream());
    ggml_cuda_kernel_launch(kernel, launch_params,
        (const char *) Q->data,
        (const char *) K->data,
        (const char *) V->data,
        mask ? (const char *) mask->data : nullptr,
        nullptr,
        nullptr,
        parts,
        meta,
        scale, max_bias, m0, m1, n_head_log2, logit_softcap,
        Q->ne[0], ne01, Q->ne[2], Q->ne[3], Q->nb[1], Q->nb[2], Q->nb[3],
        K->ne[0], K->ne[1], K->ne[2], K->ne[3], K->nb[1], K->nb[2], K->nb[3],
        V->nb[1], V->nb[2], V->nb[3],
        mask ? mask->ne[1] : 0, mask ? mask->ne[2] : 0, mask ? mask->ne[3] : 0,
        mask ? mask->nb[1] : 0, mask ? mask->nb[2] : 0, mask ? mask->nb[3] : 0);
    CUDA_CHECK(cudaGetLastError());
}

} // namespace

bool ggml_cuda_flash_attn_ext_streamed_supported(const ggml_tensor * dst, size_t stage_bytes) {
    if (dst == nullptr || dst->op != GGML_OP_FLASH_ATTN_EXT) {
        return false;
    }
    const ggml_tensor * Q = dst->src[0];
    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];
    const ggml_tensor * mask = dst->src[3];
    const ggml_tensor * sinks = dst->src[4];

    return Q != nullptr && K != nullptr && V != nullptr &&
        Q->type == GGML_TYPE_F32 && K->type == GGML_TYPE_Q8_0 && V->type == GGML_TYPE_Q4_0 &&
        Q->ne[0] == KV_STREAM_HEAD_DIM && V->ne[0] == KV_STREAM_HEAD_DIM &&
        Q->ne[1] >= 1 && Q->ne[1] <= 256 && Q->ne[3] == 1 && K->ne[3] == 1 && V->ne[3] == 1 &&
        K->ne[1] == V->ne[1] && K->ne[2] == V->ne[2] &&
        K->ne[1] % FATTN_KQ_STRIDE == 0 &&
        K->nb[0] == ggml_element_size(K) && V->nb[0] == ggml_element_size(V) &&
        K->nb[1] >= ggml_row_size(K->type, K->ne[0]) &&
        V->nb[1] >= ggml_row_size(V->type, V->ne[0]) &&
        (mask == nullptr || (mask->type == GGML_TYPE_F16 && ggml_is_contiguous(mask))) &&
        sinks == nullptr && kv_stream_block_tokens(dst, stage_bytes) > 0;
}

namespace {

static __global__ void kv_stream_record_deadline(
        const uint32_t * ready_flag,
        uint64_t * samples,
        uint64_t * misses) {
    if (threadIdx.x == 0 && blockIdx.x == 0) {
        atomicAdd(reinterpret_cast<unsigned long long *>(samples), 1ULL);
        if (*ready_flag == 0) {
            atomicAdd(reinterpret_cast<unsigned long long *>(misses), 1ULL);
        }
    }
}

static bool kv_stream_collect_timing(ggml_cuda_kv_stream_transfer_ring * ring) {
    if (!ring->timing_pending) {
        return true;
    }
    const cudaError_t status = cudaEventQuery(ring->eval_end);
    if (status == cudaErrorNotReady) {
        return false;
    }
    CUDA_CHECK(status);

    float eval_ms = 0.0f;
    CUDA_CHECK(cudaEventElapsedTime(&eval_ms, ring->eval_start, ring->eval_end));
    double busy_ratio = 0.0;
    if (ring->copy_sample_recorded && eval_ms > 0.0f && ring->current_epoch_uploads > 0) {
        float copy_ms = 0.0f;
        CUDA_CHECK(cudaEventElapsedTime(
            &copy_ms, ring->copy_sample_start, ring->copy_sample_end));
        GGML_ASSERT(ring->copy_sample_pages > 0);
        busy_ratio = std::min(1.0,
            double(copy_ms)*double(ring->current_epoch_uploads)/
                (double(ring->copy_sample_pages)*double(eval_ms)));
    }
    ring->last_copy_engine_busy_ratio = busy_ratio;
    ring->last_ring_peak_occupancy = ring->current_ring_peak_occupancy;
    ring->timing_pending = false;
    return true;
}

static bool kv_stream_graph_requests_contiguous(
        const kv_stream_graph_request & lhs,
        const kv_stream_graph_request & rhs) {
    return lhs.k_data == rhs.k_data && lhs.v_data == rhs.v_data &&
        lhs.k_nb1 == rhs.k_nb1 && lhs.k_nb2 == rhs.k_nb2 &&
        lhs.v_nb1 == rhs.v_nb1 && lhs.v_nb2 == rhs.v_nb2 &&
        lhs.n_head_kv == rhs.n_head_kv && lhs.layer == rhs.layer &&
        lhs.token_begin + lhs.token_count == rhs.token_begin &&
        lhs.token_count == rhs.token_count &&
        lhs.k_row_bytes == rhs.k_row_bytes && lhs.v_row_bytes == rhs.v_row_bytes &&
        lhs.k_head_bytes == rhs.k_head_bytes && lhs.k_bytes == rhs.k_bytes &&
        lhs.v_head_bytes == rhs.v_head_bytes && lhs.v_bytes == rhs.v_bytes;
}

static void kv_stream_graph_upload_batch(
        ggml_cuda_kv_stream_transfer_ring * ring,
        size_t request_begin,
        uint32_t slot_begin,
        uint32_t count) {
    GGML_ASSERT(count > 0 && request_begin + count <= ring->graph_requests.size());
    GGML_ASSERT(slot_begin + count <= ring->active_slots);
    auto & first = ring->graph_requests[request_begin];
    for (uint32_t i = 0; i < count; ++i) {
        auto & request = ring->graph_requests[request_begin + i];
        const uint32_t slot = slot_begin + i;
        GGML_ASSERT(request.eligible && !request.scheduled && !request.consumed);
        GGML_ASSERT(ring->slot_request[slot] == KV_STREAM_NO_REQUEST);
        GGML_ASSERT(i == 0 || kv_stream_graph_requests_contiguous(
            ring->graph_requests[request_begin + i - 1], request));
        if (ring->slot_used[slot]) {
            CUDA_CHECK(cudaStreamWaitEvent(ring->copy_stream, ring->consumed[slot], 0));
            ++ring->stage_slot_reuses;
        }
        if (request.deadline_sample) {
            CUDA_CHECK(cudaMemsetAsync(
                ring->ready_flags_device + slot, 0, sizeof(uint32_t), ring->copy_stream));
        }
    }

    // The active ring is stored as head-major K and V planes. Consecutive
    // slots are therefore a directly consumable multi-page attention span.
    char * k_stage = ring->pool_data + size_t(slot_begin)*first.k_head_bytes;
    char * v_stage = ring->pool_data +
        size_t(ring->active_slots)*first.k_bytes + size_t(slot_begin)*first.v_head_bytes;
    const size_t k_head_stride = size_t(ring->active_slots)*first.k_head_bytes;
    const size_t v_head_stride = size_t(ring->active_slots)*first.v_head_bytes;
    const size_t token_count = size_t(first.token_count)*count;
    if (ring->timing_current && !ring->copy_sample_recorded) {
        CUDA_CHECK(cudaEventRecord(ring->copy_sample_start, ring->copy_stream));
    }
    for (int64_t head = 0; head < first.n_head_kv; ++head) {
        CUDA_CHECK(cudaMemcpy2DAsync(
            k_stage + head*k_head_stride, first.k_row_bytes,
            first.k_data + head*first.k_nb2 + first.token_begin*first.k_nb1,
            first.k_nb1,
            first.k_row_bytes, token_count,
            cudaMemcpyHostToDevice, ring->copy_stream));
    }
    for (int64_t head = 0; head < first.n_head_kv; ++head) {
        CUDA_CHECK(cudaMemcpy2DAsync(
            v_stage + head*v_head_stride, first.v_row_bytes,
            first.v_data + head*first.v_nb2 + first.token_begin*first.v_nb1,
            first.v_nb1,
            first.v_row_bytes, token_count,
            cudaMemcpyHostToDevice, ring->copy_stream));
    }
    if (ring->timing_current && !ring->copy_sample_recorded) {
        CUDA_CHECK(cudaEventRecord(ring->copy_sample_end, ring->copy_stream));
        ring->copy_sample_recorded = true;
        ring->copy_sample_pages = count;
    }

    for (uint32_t i = 0; i < count; ++i) {
        auto & request = ring->graph_requests[request_begin + i];
        const uint32_t slot = slot_begin + i;
        if (request.deadline_sample) {
            CUDA_CHECK(cudaMemsetAsync(
                ring->ready_flags_device + slot, 1, sizeof(uint32_t), ring->copy_stream));
        }
        CUDA_CHECK(cudaEventRecord(ring->ready[slot], ring->copy_stream));
        request.slot = slot;
        request.scheduled = true;
        ring->slot_used[slot] = 1;
        ring->slot_request[slot] = request_begin + i;
        if (ring->graph_resident_cache != nullptr) {
            ring->graph_resident_cache->stats.host_to_device_bytes +=
                request.k_bytes + request.v_bytes;
        }
        if (ring->current_layer != KV_STREAM_NO_LAYER && request.layer > ring->current_layer) {
            ++ring->cross_layer_prefetches;
        }
    }
    ring->current_occupancy += count;
    ring->ring_peak_occupancy = std::max(
        ring->ring_peak_occupancy, ring->current_occupancy);
    ring->current_ring_peak_occupancy = std::max(
        ring->current_ring_peak_occupancy, ring->current_occupancy);
    ring->asynchronous_page_uploads += count;
    ring->current_epoch_uploads += count;
    if (count > 1) {
        ++ring->coalesced_upload_batches;
        ring->coalesced_upload_pages += count;
    }
}

static void kv_stream_graph_fill_free_slots(ggml_cuda_kv_stream_transfer_ring * ring) {
    for (uint32_t slot = 0; slot < ring->active_slots;) {
        if (ring->slot_request[slot] != KV_STREAM_NO_REQUEST) {
            ++slot;
            continue;
        }
        if (!ring->graph_active || ring->next_request >= ring->graph_requests.size() ||
                !ring->graph_requests[ring->next_request].eligible) {
            break;
        }

        uint32_t count = 1;
        while (slot + count < ring->active_slots &&
                ring->slot_request[slot + count] == KV_STREAM_NO_REQUEST &&
                ring->next_request + count < ring->graph_requests.size()) {
            const auto & previous = ring->graph_requests[ring->next_request + count - 1];
            const auto & candidate = ring->graph_requests[ring->next_request + count];
            if (!candidate.eligible ||
                    !kv_stream_graph_requests_contiguous(previous, candidate)) {
                break;
            }
            ++count;
        }
        kv_stream_graph_upload_batch(ring, ring->next_request, slot, count);
        ring->next_request += count;
        slot += count;
    }
}

static bool kv_stream_graph_layer_begin(
        ggml_cuda_kv_stream_transfer_ring * ring,
        const void * k_key,
        cudaStream_t compute_stream) {
    if (!ring->graph_active) {
        return false;
    }
    const auto layer_it = ring->graph_layer_by_k.find(k_key);
    if (layer_it == ring->graph_layer_by_k.end()) {
        return false;
    }

    ring->current_layer = layer_it->second;
    CUDA_CHECK(cudaEventRecord(ring->producer_ready, compute_stream));
    CUDA_CHECK(cudaStreamWaitEvent(ring->copy_stream, ring->producer_ready, 0));
    for (auto & request : ring->graph_requests) {
        if (request.layer == ring->current_layer && request.mutable_tail) {
            request.eligible = true;
        }
    }
    kv_stream_graph_fill_free_slots(ring);
    return true;
}

static size_t kv_stream_graph_request_index(
        const ggml_cuda_kv_stream_transfer_ring * ring,
        const void * k_key,
        uint32_t page) {
    const auto it = ring->graph_request_by_k_page.find(k_key);
    if (it == ring->graph_request_by_k_page.end() || page >= it->second.size()) {
        return KV_STREAM_NO_REQUEST;
    }
    return it->second[page];
}

static void kv_stream_graph_release_span(
        ggml_cuda_kv_stream_transfer_ring * ring,
        size_t request_begin,
        uint32_t count,
        cudaStream_t compute_stream) {
    GGML_ASSERT(count > 0 && request_begin + count <= ring->graph_requests.size());
    GGML_ASSERT(ring->current_occupancy >= count);
    for (uint32_t i = 0; i < count; ++i) {
        auto & request = ring->graph_requests[request_begin + i];
        GGML_ASSERT(request.scheduled && !request.consumed);
        const uint32_t slot = request.slot;
        GGML_ASSERT(ring->slot_request[slot] == request_begin + i);
        CUDA_CHECK(cudaEventRecord(ring->consumed[slot], compute_stream));
        request.consumed = true;
        ring->slot_request[slot] = KV_STREAM_NO_REQUEST;
    }
    ring->current_occupancy -= count;
    kv_stream_graph_fill_free_slots(ring);
}

} // namespace

void ggml_cuda_kv_stream_graph_begin(ggml_cuda_kv_stream_transfer_ring * ring) {
    GGML_ASSERT(ring != nullptr);
    const bool timing_available = kv_stream_collect_timing(ring);
    ring->graph_active = true;
    ring->graph_layer_count = 0;
    ring->current_layer = KV_STREAM_NO_LAYER;
    ring->next_request = 0;
    ring->graph_resident_cache = nullptr;
    ring->graph_requests.clear();
    ring->graph_layer_by_k.clear();
    ring->graph_request_by_k_page.clear();
    ring->current_occupancy = 0;
    if (timing_available) {
        ring->current_ring_peak_occupancy = 0;
        ring->current_epoch_uploads = 0;
        ring->copy_sample_recorded = false;
        ring->copy_sample_pages = 0;
    }
    ring->timing_current = timing_available;
    std::fill(ring->slot_request.begin(), ring->slot_request.end(), KV_STREAM_NO_REQUEST);
}

bool ggml_cuda_kv_stream_graph_add_attention(
        ggml_cuda_kv_stream_transfer_ring * ring,
        ggml_cuda_kv_stream_resident_cache * resident_cache,
        const ggml_tensor * dst) {
    GGML_ASSERT(ring != nullptr);
    if (resident_cache == nullptr || dst == nullptr ||
            !ggml_cuda_flash_attn_ext_streamed_supported(dst, ring->page_bytes)) {
        return false;
    }
    if (ring->graph_resident_cache != nullptr && ring->graph_resident_cache != resident_cache) {
        return false;
    }
    if (dst->src[0]->ne[1] != 1) {
        // Graphs are rebuilt across warmup, prompt chunks, and slot reuse.
        // Relearn pointer-to-layer identity once per prefill graph while the
        // resident page contents are refreshed by the local multi-token path.
        if (ring->graph_resident_cache == nullptr) {
            resident_cache->layer_by_k.clear();
            resident_cache->next_layer = 0;
            ring->graph_resident_cache = resident_cache;
        }
        return false;
    }
    ring->graph_resident_cache = resident_cache;
    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];
    // Resident placement is stable across evaluations, while deadlines must
    // follow this graph's finalized execution order.
    (void) kv_stream_resident_layer(resident_cache, K->data);
    const uint32_t layer = ring->graph_layer_count++;
    ring->graph_layer_by_k[K->data] = layer;

    const int64_t block_tokens = resident_cache->page_tokens;
    const int nchunks = int((K->ne[1] + block_tokens - 1)/block_tokens);
    auto & page_requests = ring->graph_request_by_k_page[K->data];
    page_requests.assign(nchunks, KV_STREAM_NO_REQUEST);

    bool sampled_first_streamed_page = false;
    for (int chunk = 0; chunk < nchunks; ++chunk) {
        const uint32_t page = uint32_t(chunk);
        if (page < resident_cache->resident_pages_per_layer) {
            continue;
        }
        const int64_t token_begin = chunk*block_tokens;
        const int64_t token_count = std::min<int64_t>(block_tokens, K->ne[1] - token_begin);
        const size_t k_row_bytes = ggml_row_size(K->type, K->ne[0]);
        const size_t v_row_bytes = ggml_row_size(V->type, V->ne[0]);

        kv_stream_graph_request request;
        request.k_data = static_cast<const char *>(K->data);
        request.v_data = static_cast<const char *>(V->data);
        request.k_nb1 = K->nb[1];
        request.k_nb2 = K->nb[2];
        request.v_nb1 = V->nb[1];
        request.v_nb2 = V->nb[2];
        request.n_head_kv = K->ne[2];
        request.token_begin = token_begin;
        request.token_count = token_count;
        request.k_row_bytes = k_row_bytes;
        request.v_row_bytes = v_row_bytes;
        request.k_head_bytes = k_row_bytes*token_count;
        request.k_bytes = request.k_head_bytes*K->ne[2];
        request.v_offset = GGML_PAD(request.k_bytes, 128);
        request.v_head_bytes = v_row_bytes*token_count;
        request.v_bytes = request.v_head_bytes*V->ne[2];
        request.layer = layer;
        request.mutable_tail = chunk == nchunks - 1;
        request.eligible = !request.mutable_tail;
        request.deadline_sample = !sampled_first_streamed_page || request.mutable_tail;
        sampled_first_streamed_page = true;
        GGML_ASSERT(request.v_offset + request.v_bytes == ring->page_bytes);

        page_requests[page] = ring->graph_requests.size();
        ring->graph_requests.push_back(request);
    }
    return true;
}

void ggml_cuda_kv_stream_graph_finalize(
        ggml_cuda_kv_stream_transfer_ring * ring, cudaStream_t compute_stream) {
    GGML_ASSERT(ring != nullptr);
    if (ring->timing_current) {
        CUDA_CHECK(cudaEventRecord(ring->eval_start, compute_stream));
    }
    kv_stream_graph_fill_free_slots(ring);
}

void ggml_cuda_kv_stream_graph_end(
        ggml_cuda_kv_stream_transfer_ring * ring, cudaStream_t compute_stream) {
    GGML_ASSERT(ring != nullptr);
    if (ring->timing_current) {
        CUDA_CHECK(cudaEventRecord(ring->eval_end, compute_stream));
        ring->timing_pending = true;
        ring->timing_current = false;
    }
}

double ggml_cuda_kv_stream_copy_engine_busy_ratio(
        ggml_cuda_kv_stream_transfer_ring * ring) {
    if (ring == nullptr) {
        return 0.0;
    }
    (void) kv_stream_collect_timing(ring);
    return ring->last_copy_engine_busy_ratio;
}

uint32_t ggml_cuda_kv_stream_last_ring_peak_occupancy(
        const ggml_cuda_kv_stream_transfer_ring * ring) {
    return ring == nullptr ? 0 : ring->last_ring_peak_occupancy;
}

void ggml_cuda_flash_attn_ext_streamed(
        ggml_backend_cuda_context & ctx,
        ggml_tensor * dst,
        ggml_cuda_kv_stream_transfer_ring * transfer_ring,
        ggml_cuda_kv_stream_resident_cache * resident_cache) {
    GGML_ASSERT(transfer_ring != nullptr);
    void * stage_data = transfer_ring->pool_data;
    const size_t stage_bytes = transfer_ring->page_bytes;
    GGML_ASSERT(ggml_cuda_flash_attn_ext_streamed_supported(dst, stage_bytes));

    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];
    const ggml_tensor * mask = dst->src[3];
    const int64_t block_tokens = resident_cache != nullptr ?
        resident_cache->page_tokens : kv_stream_block_tokens(dst, stage_bytes);
    const int nchunks = (K->ne[1] + block_tokens - 1)/block_tokens;
    const int nrows = ggml_nrows(dst);

    ggml_cuda_pool & pool = ctx.pool();
    ggml_cuda_pool_alloc<float> parts(pool, KV_STREAM_PARTS_PER_CHUNK*ggml_nelements(dst));
    ggml_cuda_pool_alloc<float2> meta(pool, KV_STREAM_PARTS_PER_CHUNK*nrows);
    ggml_cuda_pool_alloc<float> accumulator(pool, ggml_nelements(dst));
    ggml_cuda_pool_alloc<float2> accumulator_meta(pool, nrows);

    struct chunk_descriptor {
        int64_t token_begin = 0;
        int64_t token_count = 0;
        size_t k_row_bytes = 0;
        size_t v_row_bytes = 0;
        size_t k_head_bytes = 0;
        size_t k_bytes = 0;
        size_t v_offset = 0;
        size_t v_head_bytes = 0;
        size_t v_bytes = 0;
        char * k_stage = nullptr;
        char * v_stage = nullptr;
        size_t k_stage_head_stride = 0;
        size_t v_stage_head_stride = 0;
        bool upload = true;
        bool streamed = false;
        uint32_t slot = 0;
        size_t request_index = KV_STREAM_NO_REQUEST;
    };

    uint32_t resident_layer = 0;
    if (resident_cache != nullptr) {
        resident_layer = kv_stream_resident_layer(resident_cache, K->data);
    }
    const bool graph_planned = kv_stream_graph_layer_begin(
        transfer_ring, K->data, ctx.stream());

    std::vector<chunk_descriptor> chunks(nchunks);
    std::vector<size_t> streamed_chunks;
    streamed_chunks.reserve(nchunks);

    for (int chunk = 0; chunk < nchunks; ++chunk) {
        auto & desc = chunks[chunk];
        const int64_t token_begin = chunk*block_tokens;
        const int64_t token_count = std::min<int64_t>(block_tokens, K->ne[1] - token_begin);
        const size_t k_row_bytes = ggml_row_size(K->type, K->ne[0]);
        const size_t v_row_bytes = ggml_row_size(V->type, V->ne[0]);
        const size_t k_head_bytes = k_row_bytes*token_count;
        const size_t k_bytes = k_head_bytes*K->ne[2];
        const size_t v_offset = GGML_PAD(k_bytes, 128);
        const size_t v_head_bytes = v_row_bytes*token_count;
        const size_t v_bytes = v_head_bytes*V->ne[2];
        GGML_ASSERT(v_offset <= stage_bytes && v_bytes <= stage_bytes - v_offset);

        desc.token_begin = token_begin;
        desc.token_count = token_count;
        desc.k_row_bytes = k_row_bytes;
        desc.v_row_bytes = v_row_bytes;
        desc.k_head_bytes = k_head_bytes;
        desc.k_bytes = k_bytes;
        desc.v_offset = v_offset;
        desc.v_head_bytes = v_head_bytes;
        desc.v_bytes = v_bytes;
        desc.k_stage = static_cast<char *>(stage_data);
        desc.v_stage = desc.k_stage + v_offset;
        desc.k_stage_head_stride = k_head_bytes;
        desc.v_stage_head_stride = v_head_bytes;

        if (resident_cache != nullptr) {
            GGML_ASSERT(token_count == resident_cache->page_tokens);
            GGML_ASSERT(v_offset + v_bytes == resident_cache->page_bytes);

            const uint32_t page = token_begin/resident_cache->page_tokens;
            if (page < resident_cache->resident_pages_per_layer) {
                const size_t resident_index = size_t(resident_layer)*resident_cache->resident_pages_per_layer + page;
                // Keep each layer's resident K and V in separate, head-major
                // planes. A page remains the upload/dirty-tracking unit, but
                // all resident pages can now be consumed as one tensor span.
                char * layer_base = resident_cache->pool_data + resident_cache->scratch_bytes +
                    size_t(resident_layer)*resident_cache->resident_pages_per_layer*
                        resident_cache->page_bytes;
                const size_t resident_k_plane_bytes =
                    size_t(resident_cache->resident_pages_per_layer)*k_bytes;
                desc.k_stage = layer_base + size_t(page)*k_head_bytes;
                desc.v_stage = layer_base + resident_k_plane_bytes + size_t(page)*v_head_bytes;
                desc.k_stage_head_stride =
                    size_t(resident_cache->resident_pages_per_layer)*k_head_bytes;
                desc.v_stage_head_stride =
                    size_t(resident_cache->resident_pages_per_layer)*v_head_bytes;
                if (resident_cache->loaded[resident_index]) {
                    ++resident_cache->stats.resident_hits;
                    // The final page contains the rows most recently changed by SET_ROWS.
                    // Multi-token SET_ROWS may change any page covered by the
                    // prefill batch. Refresh resident pages after its producer
                    // event; only decode can safely treat non-tail pages as
                    // immutable without explicit dirty-page tracking.
                    desc.upload = dst->src[0]->ne[1] > 1 || chunk == nchunks - 1;
                } else {
                    ++resident_cache->stats.resident_misses;
                    resident_cache->loaded[resident_index] = 1;
                }
            } else {
                ++resident_cache->stats.streamed_pages;
                desc.streamed = true;
            }
        } else {
            desc.streamed = true;
        }

        if (desc.streamed) {
            const size_t stream_index = streamed_chunks.size();
            if (graph_planned) {
                desc.request_index = kv_stream_graph_request_index(
                    transfer_ring, K->data, uint32_t(chunk));
                GGML_ASSERT(desc.request_index != KV_STREAM_NO_REQUEST);
            } else {
                desc.slot = uint32_t(stream_index%transfer_ring->active_slots);
                desc.k_stage = transfer_ring->pool_data + size_t(desc.slot)*desc.k_head_bytes;
                desc.v_stage = transfer_ring->pool_data +
                    size_t(transfer_ring->active_slots)*desc.k_bytes +
                    size_t(desc.slot)*desc.v_head_bytes;
                desc.k_stage_head_stride =
                    size_t(transfer_ring->active_slots)*desc.k_head_bytes;
                desc.v_stage_head_stride =
                    size_t(transfer_ring->active_slots)*desc.v_head_bytes;
            }
            streamed_chunks.push_back(chunk);
        }
    }

    auto upload = [&](const chunk_descriptor & desc, cudaStream_t stream) {
        if (!desc.upload) {
            return;
        }
        for (int64_t head = 0; head < K->ne[2]; ++head) {
            CUDA_CHECK(cudaMemcpy2DAsync(
                desc.k_stage + head*desc.k_stage_head_stride, desc.k_row_bytes,
                static_cast<const char *>(K->data) + head*K->nb[2] + desc.token_begin*K->nb[1], K->nb[1],
                desc.k_row_bytes, desc.token_count, cudaMemcpyHostToDevice, stream));
        }
        for (int64_t head = 0; head < V->ne[2]; ++head) {
            CUDA_CHECK(cudaMemcpy2DAsync(
                desc.v_stage + head*desc.v_stage_head_stride, desc.v_row_bytes,
                static_cast<const char *>(V->data) + head*V->nb[2] + desc.token_begin*V->nb[1], V->nb[1],
                desc.v_row_bytes, desc.token_count, cudaMemcpyHostToDevice, stream));
        }
        if (resident_cache != nullptr) {
            resident_cache->stats.host_to_device_bytes += desc.k_bytes + desc.v_bytes;
        }
    };

    auto schedule_streamed = [&](size_t stream_index) {
        auto & desc = chunks[streamed_chunks[stream_index]];
        const uint32_t slot = desc.slot;
        if (transfer_ring->slot_used[slot]) {
            CUDA_CHECK(cudaStreamWaitEvent(
                transfer_ring->copy_stream, transfer_ring->consumed[slot], 0));
            ++transfer_ring->stage_slot_reuses;
        }
        upload(desc, transfer_ring->copy_stream);
        CUDA_CHECK(cudaEventRecord(transfer_ring->ready[slot], transfer_ring->copy_stream));
        transfer_ring->slot_used[slot] = 1;
        ++transfer_ring->asynchronous_page_uploads;
    };

    if (!graph_planned && !streamed_chunks.empty()) {
        // SET_ROWS and all other producers for this layer are ordered before
        // this marker on the compute stream. The copy stream may then run
        // independently while attention consumes previously prepared pages.
        CUDA_CHECK(cudaEventRecord(transfer_ring->producer_ready, ctx.stream()));
        CUDA_CHECK(cudaStreamWaitEvent(
            transfer_ring->copy_stream, transfer_ring->producer_ready, 0));
        const size_t initial = std::min<size_t>(
            transfer_ring->active_slots, streamed_chunks.size());
        for (size_t i = 0; i < initial; ++i) {
            schedule_streamed(i);
        }
    }

    size_t stream_index = 0;
    for (int chunk = 0; chunk < nchunks; ++chunk) {
        auto & desc = chunks[chunk];
        uint32_t streamed_span_pages = 0;
        if (desc.streamed) {
            streamed_span_pages = 1;
            if (graph_planned) {
                auto & request = transfer_ring->graph_requests[desc.request_index];
                GGML_ASSERT(request.scheduled && !request.consumed);
                desc.slot = request.slot;
                desc.k_stage = transfer_ring->pool_data + size_t(desc.slot)*desc.k_head_bytes;
                desc.v_stage = transfer_ring->pool_data +
                    size_t(transfer_ring->active_slots)*desc.k_bytes +
                    size_t(desc.slot)*desc.v_head_bytes;
                desc.k_stage_head_stride =
                    size_t(transfer_ring->active_slots)*desc.k_head_bytes;
                desc.v_stage_head_stride =
                    size_t(transfer_ring->active_slots)*desc.v_head_bytes;
                if (request.deadline_sample) {
                    kv_stream_record_deadline<<<1, 1, 0, ctx.stream()>>>(
                        transfer_ring->ready_flags_device + desc.slot,
                        transfer_ring->deadline_counters_device + 0,
                        transfer_ring->deadline_counters_device + 1);
                    CUDA_CHECK(cudaGetLastError());
                }
            }
            CUDA_CHECK(cudaStreamWaitEvent(ctx.stream(), transfer_ring->ready[desc.slot], 0));
            ++transfer_ring->compute_stream_waits;

            // Coalesce ready pages that occupy consecutive plane slots. The
            // head stride remains the full active-ring plane width, while the
            // tensor's token extent grows across adjacent slots.
            while (chunk + int(streamed_span_pages) < nchunks) {
                auto & candidate = chunks[chunk + streamed_span_pages];
                if (!candidate.streamed) {
                    break;
                }
                if (graph_planned) {
                    auto & request = transfer_ring->graph_requests[candidate.request_index];
                    if (!request.scheduled || request.consumed) {
                        break;
                    }
                    candidate.slot = request.slot;
                    candidate.k_stage = transfer_ring->pool_data +
                        size_t(candidate.slot)*candidate.k_head_bytes;
                    candidate.v_stage = transfer_ring->pool_data +
                        size_t(transfer_ring->active_slots)*candidate.k_bytes +
                        size_t(candidate.slot)*candidate.v_head_bytes;
                    candidate.k_stage_head_stride =
                        size_t(transfer_ring->active_slots)*candidate.k_head_bytes;
                    candidate.v_stage_head_stride =
                        size_t(transfer_ring->active_slots)*candidate.v_head_bytes;
                }
                if (candidate.slot != desc.slot + streamed_span_pages ||
                        candidate.token_begin != desc.token_begin +
                            int64_t(streamed_span_pages)*block_tokens) {
                    break;
                }
                if (graph_planned) {
                    auto & request = transfer_ring->graph_requests[candidate.request_index];
                    if (request.deadline_sample) {
                        kv_stream_record_deadline<<<1, 1, 0, ctx.stream()>>>(
                            transfer_ring->ready_flags_device + candidate.slot,
                            transfer_ring->deadline_counters_device + 0,
                            transfer_ring->deadline_counters_device + 1);
                        CUDA_CHECK(cudaGetLastError());
                    }
                }
                CUDA_CHECK(cudaStreamWaitEvent(
                    ctx.stream(), transfer_ring->ready[candidate.slot], 0));
                ++transfer_ring->compute_stream_waits;
                ++streamed_span_pages;
            }
            desc.token_count = int64_t(streamed_span_pages)*block_tokens;
            if (resident_cache != nullptr) {
                ++resident_cache->stats.streamed_attention_spans;
                resident_cache->stats.streamed_pages_attended += streamed_span_pages;
            }
        } else {
            // Resident pages form one contiguous head-major K/V prefix. Upload
            // dirty pages separately, then execute the whole prefix once.
            if (chunk > 0) {
                continue;
            }
            const uint32_t resident_span_pages = std::min<uint32_t>(
                uint32_t(nchunks), resident_cache->resident_pages_per_layer);
            GGML_ASSERT(resident_span_pages > 0);
            for (uint32_t page = 0; page < resident_span_pages; ++page) {
                upload(chunks[page], ctx.stream());
            }
            desc.token_count = int64_t(resident_span_pages)*block_tokens;
            desc.k_head_bytes = desc.k_row_bytes*desc.token_count;
            desc.k_bytes = desc.k_head_bytes*K->ne[2];
            desc.v_head_bytes = desc.v_row_bytes*desc.token_count;
            desc.v_bytes = desc.v_head_bytes*V->ne[2];
            ++resident_cache->stats.resident_attention_spans;
            resident_cache->stats.resident_pages_attended += resident_span_pages;
        }

        ggml_tensor staged_k = *K;
        ggml_tensor staged_v = *V;
        staged_k.data = desc.k_stage;
        staged_k.ne[1] = desc.token_count;
        staged_k.nb[1] = desc.k_row_bytes;
        staged_k.nb[2] = desc.k_stage_head_stride;
        staged_k.nb[3] = desc.k_stage_head_stride*K->ne[2];
        staged_v.data = desc.v_stage;
        staged_v.ne[1] = desc.token_count;
        staged_v.nb[1] = desc.v_row_bytes;
        staged_v.nb[2] = desc.v_stage_head_stride;
        staged_v.nb[3] = desc.v_stage_head_stride*V->ne[2];

        ggml_tensor staged_mask{};
        ggml_tensor * staged_mask_ptr = nullptr;
        if (mask != nullptr) {
            staged_mask = *mask;
            staged_mask.data = static_cast<char *>(mask->data) + desc.token_begin*mask->nb[0];
            staged_mask.ne[0] = desc.token_count;
            staged_mask_ptr = &staged_mask;
        }

        ggml_tensor staged_dst = *dst;
        staged_dst.src[1] = &staged_k;
        staged_dst.src[2] = &staged_v;
        staged_dst.src[3] = staged_mask_ptr;

        // Preserve the normal CUDA flash-attention path when the active cache
        // fits in a single page. Besides avoiding an unnecessary partial
        // reduction, this keeps short-context logits numerically identical to
        // a non-streamed cache.
        if (nchunks == 1) {
            ggml_cuda_flash_attn_ext(ctx, &staged_dst);
            if (desc.streamed) {
                if (graph_planned) {
                    kv_stream_graph_release_span(
                        transfer_ring, desc.request_index, 1, ctx.stream());
                } else {
                    CUDA_CHECK(cudaEventRecord(transfer_ring->consumed[desc.slot], ctx.stream()));
                }
            }
            return;
        }

        float logit_softcap = 0.0f;
        memcpy(&logit_softcap, (const float *) dst->op_params + 2, sizeof(float));
        if (logit_softcap == 0.0f) {
            kv_stream_launch_q8_q4_partial<false>(ctx, &staged_dst, parts.ptr, meta.ptr);
        } else {
            kv_stream_launch_q8_q4_partial<true>(ctx, &staged_dst, parts.ptr, meta.ptr);
        }

        const dim3 blocks(nrows, 1, 1);
        const dim3 threads(KV_STREAM_HEAD_DIM, 1, 1);
        const ggml_cuda_kernel_launch_params launch_params(blocks, threads, 0, ctx.stream());
        ggml_cuda_kernel_launch(kv_stream_accumulate_chunk_results<KV_STREAM_HEAD_DIM>, launch_params,
            parts.ptr, meta.ptr, accumulator.ptr, accumulator_meta.ptr, nrows, chunk == 0);
        CUDA_CHECK(cudaGetLastError());

        if (desc.streamed) {
            if (graph_planned) {
                kv_stream_graph_release_span(
                    transfer_ring, desc.request_index, streamed_span_pages, ctx.stream());
            }
            for (uint32_t page = 0; page < streamed_span_pages; ++page) {
                auto & member = chunks[chunk + page];
                if (!graph_planned) {
                    CUDA_CHECK(cudaEventRecord(
                        transfer_ring->consumed[member.slot], ctx.stream()));
                    const size_t next = stream_index + page + transfer_ring->active_slots;
                    if (next < streamed_chunks.size()) {
                        schedule_streamed(next);
                    }
                }
            }
            stream_index += streamed_span_pages;
            chunk += int(streamed_span_pages) - 1;
        }
    }

    const dim3 blocks(nrows, 1, 1);
    const dim3 threads(KV_STREAM_HEAD_DIM, 1, 1);
    const ggml_cuda_kernel_launch_params launch_params(blocks, threads, 0, ctx.stream());
    ggml_cuda_kernel_launch(kv_stream_normalize_chunk_results<KV_STREAM_HEAD_DIM>, launch_params,
        accumulator.ptr, accumulator_meta.ptr, static_cast<float *>(dst->data), nrows);
    CUDA_CHECK(cudaGetLastError());
}

template <int DKQ, int DV, int ncols2>
static void ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const int cc = ggml_cuda_info().devices[ggml_cuda_get_device()].cc;
    const ggml_tensor * Q = dst->src[0];

#if !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)
    if constexpr (ggml_cuda_flash_attn_ext_mma_f16_may_use_sparse(DKQ, DV, 1, ncols2)) {
        if (ggml_cuda_flash_attn_ext_mma_f16_shall_use_sparse(ctx, dst)) {
            ggml_cuda_flash_attn_ext_mma_f16_case<DKQ, DV, 1, ncols2>(ctx, dst);
            return;
        }
    }
#endif // !defined(GGML_USE_HIP) && !defined(GGML_USE_MUSA)

    if constexpr (ncols2 <= 8) {
        if (turing_mma_available(cc) && Q->ne[1] <= 8/ncols2) {
            ggml_cuda_flash_attn_ext_mma_f16_case<DKQ, DV, 8/ncols2, ncols2>(ctx, dst);
            return;
        }
    }

    if constexpr (ncols2 <= 16) {
        if (Q->ne[1] <= 16/ncols2) {
            ggml_cuda_flash_attn_ext_mma_f16_case<DKQ, DV, 16/ncols2, ncols2>(ctx, dst);
            return;
        }
    }

    if (Q->ne[1] <= 32/ncols2 || (GGML_CUDA_CC_IS_NVIDIA(cc) && ggml_cuda_highest_compiled_arch(cc) == GGML_CUDA_CC_TURING) ||
            (GGML_CUDA_CC_IS_AMD(cc) && DKQ > 256)) {
        ggml_cuda_flash_attn_ext_mma_f16_case<DKQ, DV, 32/ncols2, ncols2>(ctx, dst);
        return;
    }

    ggml_cuda_flash_attn_ext_mma_f16_case<DKQ, DV, 64/ncols2, ncols2>(ctx, dst);
}

template <int DKQ, int DV>
static void ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const int cc = ggml_cuda_info().devices[ggml_cuda_get_device()].cc;
    const ggml_tensor * KQV  = dst;
    const ggml_tensor * Q    = dst->src[0];
    const ggml_tensor * K    = dst->src[1];
    const ggml_tensor * V    = dst->src[2];
    const ggml_tensor * mask = dst->src[3];

    float max_bias = 0.0f;
    memcpy(&max_bias, (const float *) KQV->op_params + 1, sizeof(float));

    // Edge cases like no mask, ALiBi, unpadded K/V, or misaligned addresses for large data transfers
    //     are put into the template specialization without GQA optimizations.
    bool use_gqa_opt = mask && max_bias == 0.0f && K->ne[1] % FATTN_KQ_STRIDE == 0;
    for (const ggml_tensor * t : {Q, K, V, mask}) {
        if (t == nullptr || ggml_is_quantized(t->type)) {
            continue;
        }
        for (size_t i = 1; i < GGML_MAX_DIMS; ++i) {
            if (t->nb[i] % 16 != 0) {
                use_gqa_opt = false;
                break;
            }
        }
    }

    GGML_ASSERT(Q->ne[2] % K->ne[2] == 0);
    const int gqa_ratio = Q->ne[2] / K->ne[2];

    // On Volta the GQA optimizations aren't as impactful vs. minimizing wasted compute:
    if (cc == GGML_CUDA_CC_VOLTA) {
        if (use_gqa_opt && gqa_ratio % 8 == 0) {
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 8>(ctx, dst);
            return;
        }

        if (use_gqa_opt && gqa_ratio % 4 == 0) {
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 4>(ctx, dst);
            return;
        }

        if constexpr (DKQ <= 256) {
            if (use_gqa_opt && gqa_ratio % 2 == 0) {
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 2>(ctx, dst);
                return;
            }

            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 1>(ctx, dst);
            return;
        } else {
            GGML_ABORT("fatal error");
        }
    }

    if (use_gqa_opt && gqa_ratio > 4) {
        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 8>(ctx, dst);
        return;
    }

    if (use_gqa_opt && gqa_ratio > 2) {
        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 4>(ctx, dst);
        return;
    }

    if (use_gqa_opt && gqa_ratio > 1) {
        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 2>(ctx, dst);
        return;
    }

    if constexpr (DKQ <= 256) {
        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<DKQ, DV, 1>(ctx, dst);
    } else {
        GGML_ABORT("fatal error");
    }
}

static void ggml_cuda_flash_attn_ext_mma_f16(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const int cc = ggml_cuda_info().devices[ggml_cuda_get_device()].cc;
    const ggml_tensor * KQV  = dst;
    const ggml_tensor * Q    = dst->src[0];
    const ggml_tensor * K    = dst->src[1];
    const ggml_tensor * V    = dst->src[2];
    const ggml_tensor * mask = dst->src[3];

    switch (Q->ne[0]) {
        case 64:
            GGML_ASSERT(V->ne[0] == 64);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2< 64,  64>(ctx, dst);
            break;
        case 80:
            GGML_ASSERT(V->ne[0] == 80);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2< 80,  80>(ctx, dst);
            break;
        case 96:
            GGML_ASSERT(V->ne[0] == 96);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2< 96,  96>(ctx, dst);
            break;
        case 112:
            GGML_ASSERT(V->ne[0] == 112);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2<112, 112>(ctx, dst);
            break;
        case 128:
            GGML_ASSERT(V->ne[0] == 128);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2<128, 128>(ctx, dst);
            break;
        case 192: {
            // MiMo-V2.5 / V2.5-Pro / V2-Flash: gqa_ratio is 8 (SWA) or 16 (full attn)
            GGML_ASSERT(V->ne[0] == 128);
            float max_bias = 0.0f;
            memcpy(&max_bias, (const float *) KQV->op_params + 1, sizeof(float));
            const bool use_gqa_opt = mask && max_bias == 0.0f;
            GGML_ASSERT(use_gqa_opt);
            GGML_ASSERT(Q->ne[2] % K->ne[2] == 0);
            const int gqa_ratio = Q->ne[2] / K->ne[2];
            if (gqa_ratio % 16 == 0) {
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<192, 128, 16>(ctx, dst);
            } else {
                GGML_ASSERT(gqa_ratio % 8 == 0);
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<192, 128,  8>(ctx, dst);
            }
        } break;
        case 256:
            GGML_ASSERT(V->ne[0] == 256);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2<256, 256>(ctx, dst);
            break;
        case 320:
            // For Mistral Small 4, go straight to the ncols1 switch (ncols2=32-only build).
            GGML_ASSERT(V->ne[0] == 256);
            {
                float max_bias = 0.0f;
                memcpy(&max_bias, (const float *) KQV->op_params + 1, sizeof(float));

                const bool use_gqa_opt = mask && max_bias == 0.0f;
                GGML_ASSERT(use_gqa_opt);
                GGML_ASSERT(Q->ne[2] % K->ne[2] == 0);
                const int gqa_ratio = Q->ne[2] / K->ne[2];
                GGML_ASSERT(gqa_ratio % 32 == 0);

                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<320, 256, 32>(ctx, dst);
            }
            break;
        case 512:
            GGML_ASSERT(V->ne[0] == 512);
            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols2<512, 512>(ctx, dst);
            break;
        case 576: {
            // For Deepseek, go straight to the ncols1 switch to avoid compiling unnecessary kernels.
            GGML_ASSERT(V->ne[0] == 512);
            float max_bias = 0.0f;
            memcpy(&max_bias, (const float *) KQV->op_params + 1, sizeof(float));

            const bool use_gqa_opt = mask && max_bias == 0.0f;
            GGML_ASSERT(use_gqa_opt);

            GGML_ASSERT(Q->ne[2] % K->ne[2] == 0);
            const int gqa_ratio = Q->ne[2] / K->ne[2];
            if (gqa_ratio == 20) { // GLM 4.7 Flash
                if (cc >= GGML_CUDA_CC_DGX_SPARK) {
                    if (Q->ne[1] <= 8) {
                        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 16>(ctx, dst);
                        break;
                    }
                    ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 4>(ctx, dst);
                    break;
                }
                if (cc >= GGML_CUDA_CC_BLACKWELL) {
                    if (Q->ne[1] <= 4 && K->ne[1] >= 65536) {
                        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 16>(ctx, dst);
                        break;
                    }
                    ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 4>(ctx, dst);
                    break;
                }
                if (cc >= GGML_CUDA_CC_ADA_LOVELACE) {
                    if (Q->ne[1] <= 4) {
                        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 16>(ctx, dst);
                        break;
                    }
                    ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 4>(ctx, dst);
                    break;
                }
                if (cc >= GGML_CUDA_CC_TURING) {
                    if (Q->ne[1] <= 4) {
                        if (K->ne[1] <= 16384) {
                            ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 16>(ctx, dst);
                            break;
                        }
                        ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 32>(ctx, dst);
                        break;
                    }
                    ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 4>(ctx, dst);
                    break;
                }
                // Volta:
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 4>(ctx, dst);
            } else if (gqa_ratio % 16 == 0) {
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512, 16>(ctx, dst);
            } else {
                ggml_cuda_flash_attn_ext_mma_f16_switch_ncols1<576, 512,  4>(ctx, dst);
            }
        } break;
        default:
            GGML_ABORT("fatal error");
            break;
    }
}

#define FATTN_VEC_CASE(D, type_K, type_V)                                                                        \
    {                                                                                                            \
        const bool type_K_okay = K->type == (type_K) || (K->type == GGML_TYPE_F32 && (type_K) == GGML_TYPE_F16); \
        const bool type_V_okay = V->type == (type_V) || (V->type == GGML_TYPE_F32 && (type_V) == GGML_TYPE_F16); \
        if (Q->ne[0] == (D) && type_K_okay && type_V_okay) {                                                     \
            ggml_cuda_flash_attn_ext_vec_case<D, type_K, type_V>(ctx, dst);                                      \
            return;                                                                                              \
        }                                                                                                        \
    }                                                                                                            \

#define FATTN_VEC_CASES_ALL_D(type_K, type_V) \
    FATTN_VEC_CASE( 64, type_K, type_V)       \
    FATTN_VEC_CASE(128, type_K, type_V)       \
    FATTN_VEC_CASE(256, type_K, type_V)       \

static void ggml_cuda_flash_attn_ext_vec(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    ggml_tensor * Q = dst->src[0];
    ggml_tensor * K = dst->src[1];
    ggml_tensor * V = dst->src[2];

#ifdef GGML_CUDA_FA_ALL_QUANTS
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_F16)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_Q4_0)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q4_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_Q4_1)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q5_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_Q5_0)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q5_1)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_Q5_1)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_Q8_0)

    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_1, GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_0, GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q5_1, GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_BF16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_BF16)
#else
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_F16,  GGML_TYPE_F16)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q4_0, GGML_TYPE_Q4_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_Q8_0, GGML_TYPE_Q8_0)
    FATTN_VEC_CASES_ALL_D(GGML_TYPE_BF16, GGML_TYPE_BF16)
#endif // GGML_CUDA_FA_ALL_QUANTS

    GGML_ABORT("fatal error");
}

// Best FlashAttention kernel for a specific GPU:
enum best_fattn_kernel {
    BEST_FATTN_KERNEL_NONE    =   0,
    BEST_FATTN_KERNEL_TILE    = 200,
    BEST_FATTN_KERNEL_VEC     = 100,
    BEST_FATTN_KERNEL_MMA_F16 = 400,
};

static bool ggml_cuda_fattn_kv_type_supported(ggml_type type) {
    switch (type) {
        case GGML_TYPE_F32:
        case GGML_TYPE_F16:
            return true;
        case GGML_TYPE_Q4_1:
        case GGML_TYPE_Q5_0:
        case GGML_TYPE_Q5_1:
#ifndef GGML_CUDA_FA_ALL_QUANTS
            return false;
#endif // GGML_CUDA_FA_ALL_QUANTS
        case GGML_TYPE_Q4_0:
        case GGML_TYPE_Q8_0:
        case GGML_TYPE_BF16:
            return true;
        default:
            return false;
    }
}

static best_fattn_kernel ggml_cuda_get_best_fattn_kernel(const int device, const ggml_tensor * dst) {
#ifndef FLASH_ATTN_AVAILABLE
    GGML_UNUSED(device); GGML_UNUSED(dst);
    return BEST_FATTN_KERNEL_NONE;
#endif// FLASH_ATTN_AVAILABLE

    const ggml_tensor * KQV   = dst;
    const ggml_tensor * Q     = dst->src[0];
    const ggml_tensor * K     = dst->src[1];
    const ggml_tensor * V     = dst->src[2];
    const ggml_tensor * mask  = dst->src[3];

    const int gqa_ratio = Q->ne[2] / K->ne[2];
    GGML_ASSERT(Q->ne[2] % K->ne[2] == 0);

    float max_bias = 0.0f;
    memcpy(&max_bias, (const float *) KQV->op_params + 1, sizeof(float));

    // The effective batch size for the kernel can be increased by gqa_ratio.
    // The kernel versions without this optimization are also used for ALiBi, if there is no mask, or if the KV cache is not padded,
    bool gqa_opt_applies = gqa_ratio >= 2 && mask && max_bias == 0.0f && K->ne[1] % FATTN_KQ_STRIDE == 0;
    for (const ggml_tensor * t : {Q, K, V, mask}) {
        if (t == nullptr || ggml_is_quantized(t->type)) {
            continue;
        }
        for (size_t i = 1; i < GGML_MAX_DIMS; ++i) {
            if (t->nb[i] % 16 != 0) {
                gqa_opt_applies = false;
                break;
            }
        }
    }

    const int cc = ggml_cuda_info().devices[device].cc;

    switch (K->ne[0]) {
        case  40:
        case  64:
        case  72:
        case  80:
        case  96:
        case 128:
        case 112:
        case 256:
            if (V->ne[0] != K->ne[0]) {
                return BEST_FATTN_KERNEL_NONE;
            }
            break;
        case 192:
            if (V->ne[0] != 128 || !gqa_opt_applies) {
                return BEST_FATTN_KERNEL_NONE;
            }
            if (gqa_ratio % 8 != 0) {
                return BEST_FATTN_KERNEL_NONE;
            }
            break;
        case 320:
            if (V->ne[0] != 256 || !gqa_opt_applies) {
                return BEST_FATTN_KERNEL_NONE;
            }
            if (gqa_ratio % 32 != 0) {
                return BEST_FATTN_KERNEL_NONE;
            }
            break;
        case 512:
            if (V->ne[0] != K->ne[0]) {
                return BEST_FATTN_KERNEL_NONE;
            }
            if (!gqa_opt_applies) {
                return BEST_FATTN_KERNEL_NONE;
            }
            break;
        case 576:
            if (V->ne[0] != 512) {
                return BEST_FATTN_KERNEL_NONE;
            }
            if (!gqa_opt_applies) {
                return BEST_FATTN_KERNEL_NONE;
            }
            break;
        default:
            return BEST_FATTN_KERNEL_NONE;
    }

#ifndef GGML_CUDA_FA_ALL_QUANTS
    if (K->type != V->type) {
        return BEST_FATTN_KERNEL_NONE;
    }
#endif // GGML_CUDA_FA_ALL_QUANTS

    if (!ggml_cuda_fattn_kv_type_supported(K->type) || !ggml_cuda_fattn_kv_type_supported(V->type)) {
        return BEST_FATTN_KERNEL_NONE;
    }

    if (mask && mask->ne[2] != 1) {
        return BEST_FATTN_KERNEL_NONE;
    }

    // For small batch sizes the vector kernel may be preferable over the kernels optimized for large batch sizes:
    // 192 satisfies % 64 == 0 but has no vec instance (DKQ != DV); force it onto the MMA path.
    const bool can_use_vector_kernel = Q->ne[0] <= 256 && Q->ne[0] % 64 == 0 && Q->ne[0] != 192 && K->ne[1] % FATTN_KQ_STRIDE == 0;

    // If Turing tensor cores are available, use them:
    if (turing_mma_available(cc) && Q->ne[0] != 40 && Q->ne[0] != 72) {
        if (can_use_vector_kernel) {
            if (!ggml_is_quantized(K->type) && !ggml_is_quantized(V->type)) {
                if (cc >= GGML_CUDA_CC_ADA_LOVELACE && Q->ne[1] == 1 && Q->ne[3] == 1 && !(gqa_ratio > 4 && K->ne[1] >= 8192)) {
                    return BEST_FATTN_KERNEL_VEC;
                }
            } else {
                if (cc >= GGML_CUDA_CC_ADA_LOVELACE) {
                    if (Q->ne[1] <= 2) {
                        return BEST_FATTN_KERNEL_VEC;
                    }
                } else {
                    if (Q->ne[1] == 1) {
                        return BEST_FATTN_KERNEL_VEC;
                    }
                }
            }
            if (!gqa_opt_applies && Q->ne[1] == 1) {
                return BEST_FATTN_KERNEL_VEC;
            }
        }
        return BEST_FATTN_KERNEL_MMA_F16;
    }

    const int ncols2_max = Q->ne[0] == 320 ? 32 : ((Q->ne[0] == 576 || Q->ne[0] == 192) ? 16 : 8);
    int gqa_ratio_eff = 1;
    while (gqa_ratio % (2*gqa_ratio_eff) == 0 && gqa_ratio_eff < ncols2_max) {
        gqa_ratio_eff *= 2;
    }

    if (volta_mma_available(cc) && Q->ne[0] != 40 && Q->ne[0] != 72) {
        if (can_use_vector_kernel && Q->ne[1] * gqa_ratio_eff <= 2) {
            return BEST_FATTN_KERNEL_VEC;
        }
        if (Q->ne[1] * gqa_ratio_eff <= 16) {
            return BEST_FATTN_KERNEL_TILE; // On Volta tensor cores are only faster for sufficiently large matrices.
        }
        return BEST_FATTN_KERNEL_MMA_F16;
    }

    // AMD MFMA needs a certain minimum batch size to outscale the tile kernel for large head sizes.
    if ((amd_mfma_available(cc) && Q->ne[0] <= 256) && Q->ne[0] != 40 && Q->ne[0] != 72) {
        if ((Q->ne[0] <= 64 && Q->ne[1] * gqa_ratio_eff > 8)) {
            return BEST_FATTN_KERNEL_MMA_F16;
        }
        if ((Q->ne[0] <= 128 && Q->ne[1] * gqa_ratio_eff > 16)) {
            return BEST_FATTN_KERNEL_MMA_F16;
        }
        if ((Q->ne[0] <= 256 && Q->ne[1] * gqa_ratio_eff > 64)) {
            return BEST_FATTN_KERNEL_MMA_F16;
        }
    }

    // AMD WMMA is always faster than the tile kernel if the full tile width of 16 can be utilized.
    if ((amd_wmma_available(cc) && gqa_opt_applies && Q->ne[0] <= 128) && Q->ne[0] != 40 && Q->ne[0] != 72 && Q->ne[1] * gqa_ratio_eff > 8) {
        return BEST_FATTN_KERNEL_MMA_F16;
    }

    // If there are no tensor cores available, use the generic tile kernel:
    if (can_use_vector_kernel) {
        if (!ggml_is_quantized(K->type) && !ggml_is_quantized(V->type)) {
            if (Q->ne[1] == 1) {
                if (!gqa_opt_applies) {
                    return BEST_FATTN_KERNEL_VEC;
                }
            }
        } else {
            if (Q->ne[1] <= 2) {
                return BEST_FATTN_KERNEL_VEC;
            }
        }
    }
    return BEST_FATTN_KERNEL_TILE;
}

size_t ggml_cuda_flash_attn_ext_get_alloc_size(int device, const ggml_tensor * dst) {
    GGML_ASSERT(dst->op == GGML_OP_FLASH_ATTN_EXT);

    const ggml_tensor * K = dst->src[1];
    const ggml_tensor * V = dst->src[2];

    GGML_ASSERT(K != nullptr);
    GGML_ASSERT(V != nullptr);

    const best_fattn_kernel kernel = ggml_cuda_get_best_fattn_kernel(device, dst);

    bool need_f16_K = false;
    bool need_f16_V = false;

    switch (kernel) {
        case BEST_FATTN_KERNEL_TILE:
        case BEST_FATTN_KERNEL_MMA_F16:
            need_f16_K = true;
            need_f16_V = true;
            break;
        case BEST_FATTN_KERNEL_VEC:
            need_f16_K = K->type == GGML_TYPE_F32;
            need_f16_V = V->type == GGML_TYPE_F32;
            break;
        case BEST_FATTN_KERNEL_NONE:
            break;
    }

    const ggml_cuda_flash_attn_ext_f16_extra_data f16_extra =
        ggml_cuda_flash_attn_ext_get_f16_extra_data(dst, need_f16_K, need_f16_V);

    return f16_extra.end - (uintptr_t) dst->data;
}

void ggml_cuda_flash_attn_ext(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    ggml_cuda_set_device(ctx.device);
    switch (ggml_cuda_get_best_fattn_kernel(ggml_cuda_get_device(), dst)) {
        case BEST_FATTN_KERNEL_NONE:
            GGML_ABORT("fatal error");
        case BEST_FATTN_KERNEL_TILE:
            ggml_cuda_flash_attn_ext_tile(ctx, dst);
            break;
        case BEST_FATTN_KERNEL_VEC:
            ggml_cuda_flash_attn_ext_vec(ctx, dst);
            break;
        case BEST_FATTN_KERNEL_MMA_F16:
            ggml_cuda_flash_attn_ext_mma_f16(ctx, dst);
            break;
    }
}

bool ggml_cuda_flash_attn_ext_supported(int device, const ggml_tensor * dst) {
    return ggml_cuda_get_best_fattn_kernel(device, dst) != BEST_FATTN_KERNEL_NONE;
}
